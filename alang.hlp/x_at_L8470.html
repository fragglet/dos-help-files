<html>

<head>
<title>Loops - Assembly Language Help</title>
<link rel="stylesheet" href="../style.css">
</head>

<body>
<div class="page-heading">
<form action="https://duckduckgo.com/" method="GET">
<input name="q" id="search-box" placeholder="Search">
<input type="hidden" name="sites" value="fragglet.github.io">
</form>
Assembly Language Help (alang.hlp) (<a href='index.html'>Table of Contents</a>; <a href='TOPIC_LIST.html'>Topic list</a>)</div>
<div class="page-title"> Loops</div>
<pre>                                             <span class='grhilite'>◄</span><a href='x_at_L843b.html'>Up</a><span class='grhilite'>►</span> <span class='grhilite'>◄</span><a href='../pwb.hlp/hv.html'>Contents</a><span class='grhilite'>►</span> <span class='grhilite'>◄</span><a href='../pwb.hlp/vx.html'>Index</a><span class='grhilite'>►</span> <span class='grhilite'>◄</span><a href='javascript:history.back();'>Back</a><span class='grhilite'>►</span>
────────────────────────────────────────────────────────────────────────────
 
;* LOOPS.ASM - Illustrates directives that control looping. The
;* following directives can be used in place of loops generated
;* with the LOOP, LOOPNE, LOOPE, JCXC, and conditional jump
;* instructions. See <a href='x_at_L849c.html'>LOOPSO.ASM</a> for an example of old-style loops.
;*
;* Shows:  Directives   - .REPEAT   .UNTIL    .UNTILCXZ
;*                        .WHILE    .ENDW
;*                        .BREAK    .CONTINUE
;*         DOS function - 07h (Raw Input, No Echo)
 
        .MODEL  small, c, os_dos        ; Small model (could be any model)
        .DOSSEG                         ; Force DOS segment order
        .STACK
        .DATA
msg1    BYTE    13, 10, "Iteration: "   ; Iteration message
num1    BYTE    0, "$"                  ; Place holder for iteration byte
msg2    BYTE    13, 10, "Alphabet: $"   ; Alphabet message
num2    BYTE    0, " $"                 ; Place holder
msg3    BYTE    13, 10, "Type digits, then press ENTER: $"
 
        .CODE
        .STARTUP                        ; Initialize
 
; A simple loop that repeats until CX is zero. This is equivalent to a
; LOOP instruction.
 
        mov     cx, 9
        sub     bl, bl
        .REPEAT
        mov     bl, 10                  ; Calculate iteration number
        sub     bl, cl
        mov     num1, bl                ; Copy number to memory
        add     num1, '0'               ; Convert to ASCII
 
        mov     ah, 09h                 ; Request DOS Function 9
        mov     dx, OFFSET msg1         ; Load offset of string
        int     21h                     ; Display string
        .UNTILCXZ
 
; This conditional loop does its test at the end. It will always execute
; at least once, regardless of entry conditions.
 
        mov     dx, OFFSET msg2         ; Load offset of string
        int     21h                     ; Display string
        mov     bl, 'a'
        .REPEAT                         ; While lowercase letter:
        mov     num2, bl                ; Copy letter to memory
        inc     bl                      ; Next letter
        mov     dl, bl                  ; Load for display
 
        mov     ah, 09h                 ; Request DOS Function 9
        mov     dx, OFFSET num2         ; Load offset of string
        int     21h                     ; Display string
        .UNTIL  bl &gt; 'z'
 
        mov     ah, 09h                 ; Request DOS Function 9
        mov     dx, OFFSET msg3         ; Load offset of string
        int     21h                     ; Display string
 
; A common loop in assembler is one with the conditional test in the
; middle. This loop has two tests--one that breaks and one that
; continues to the top of the loop.
 
        .WHILE  1                       ; Loop forever (or until break)
        mov     ah, 07h                 ; Get key without echo
        int     21h
 
        .BREAK  .IF al == 13            ; Terminate if ENTER
        .CONTINUE .IF (al &lt; '0') || (al &gt; '9')  ; Skip if not digit
 
        mov     dl, al                  ; Copy
        mov     ah, 02h                 ; Output character
        int     21h
        .ENDW
 
        .EXIT   0                       ; Exit  with return code 0
 
        END
                                    -♦-
</pre>
</body>

</html>
