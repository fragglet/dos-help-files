<html>

<head>
<title>DECLARE Statement (BASIC Procedures) Programming Example - bas7ex.hlp</title>
<link rel="stylesheet" href="../style.css">
<link rel="shortcut icon" href="../helpicon.png">
</head>

<body>
<div class="page-heading">
<form action="https://duckduckgo.com/" method="GET">
<input name="q" id="search-box" placeholder="Search">
<input type="hidden" name="sites" value="fragglet.github.io">
</form>
bas7ex.hlp (<a href='TOPIC_LIST.html'>Topic list</a>)</div>
<div class="page-title"> DECLARE Statement (BASIC Procedures) Programming Example</div>
<pre>                       <span class='grhilite'>◄</span>Example<span class='grhilite'>►</span>                 <span class='grhilite'>◄</span><a href='unknown#-9996'>Contents</a><span class='grhilite'>►</span>  <span class='grhilite'>◄</span><a href='unknown#-9997'>Index</a><span class='grhilite'>►</span>  <span class='grhilite'>◄</span><a href='javascript:history.back();'>Back</a><span class='grhilite'>►</span>
──────────────────────────────────────────────────────────────────────────────
'Example 1 uses the DECLARE statement to allow a SUB procedure to be
'invoked without using the CALL keyword.
'
'Example 2 uses the BYVAL keyword to pass an argument by value.
'
'*************************************************************
'* Example 1:
'*************************************************************
 
DECLARE SUB Sort (A() AS INTEGER, N AS INTEGER)
DIM Array1(1 TO 20) AS INTEGER
DIM I AS INTEGER
'Generate 20 random numbers.
RANDOMIZE TIMER
FOR I% = 1 TO 20
   Array1(I%) = INT(RND * 100)
NEXT I%
 
'Sort the array, calling Sort without the CALL keyword. Notice the
'absence of parentheses around the arguments in the call to Sort.
Sort Array1(), 20
 
'Print the sorted array.
CLS
FOR I% = 1 TO 20
   PRINT Array1(I%)
NEXT I%
END
 
 
'Sort procedure.
SUB Sort (A%(), N%)
 
   FOR I% = 1 TO N% - 1
      FOR J% = I% + 1 TO N%
         IF A%(I%) &gt; A%(J%) THEN SWAP A%(I%), A%(J%)
      NEXT J%
   NEXT I%
 
END SUB
 
'***************************************************************
'* Example 2:
'***************************************************************
 
DECLARE SUB Mult (BYVAL X!, Y!)
CLS
a = 1
b = 1
PRINT "Before subprogram call, A = "; a; ", B ="; b
 
' A is passed by value,  and B is passed by reference:
CALL Mult(a, b)
PRINT "After subprogram call, A ="; a; ", B ="; b
 
END
 
SUB Mult (BYVAL X, Y)
        X = 2 * X
        Y = 3 * Y
        PRINT "In subprogram, X ="; X; " , Y ="; Y
END SUB
 
 
</pre>
</body>

</html>
