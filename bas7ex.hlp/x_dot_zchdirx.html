<html>

<head>
<title>CHDIR and MKDIR Programming Example - bas7ex.hlp</title>
<link rel="stylesheet" href="../style.css">
<link rel="shortcut icon" href="../helpicon.png">
</head>

<body>
<div class="page-heading">
<form action="https://www.google.com/search" method="GET">
<input name="as_q" id="search-box" placeholder="Search">
<input type="hidden" name="as_sitesearch" value="fragglet.github.io/dos-help-files/*">
</form>
bas7ex.hlp (<a href='TOPIC_LIST.html'>Topic list</a>)</div>
<div class="page-title"> CHDIR and MKDIR Programming Example</div>
<pre>                       <span class='grhilite'>◄</span>Example<span class='grhilite'>►</span>                 <span class='grhilite'>◄</span><a href='../unknown.html#-9996'>Contents</a><span class='grhilite'>►</span>  <span class='grhilite'>◄</span><a href='../unknown.html#-9997'>Index</a><span class='grhilite'>►</span>  <span class='grhilite'>◄</span><a href='javascript:history.back();'>Back</a><span class='grhilite'>►</span>
──────────────────────────────────────────────────────────────────────────────
'This example uses the MKDIR statement to create a subdirectory. The
'AddADir procedure uses the CHDIR statement with the ON ERROR statement
'to chek if the subdirectory already exists. If it does not exist, the
'error-handling routine traps error 76, prompts the user, and creates
'the directory.
 
DECLARE SUB AddADir (DestDir$)
 
CLS
AddADir "TESTDIR"
 
SUB AddADir (DestDir$)
ON LOCAL ERROR GOTO ErrHandler  'Set up the local error handler.
    CurDirName$ = CURDIR$       'Preserve the name of the current directory.
    CHDIR DestDir$              'Change to DestDir$ - Error 76 if not exist.
    CHDIR CurDirName$           'Change back to original directory.
    EXIT SUB
 
ErrHandler:
    IF ERR = 76 THEN
        PRINT "Directory does not exist. Create ?";
        Ans$ = INPUT$(1)
        PRINT Ans$
        IF UCASE$(Ans$) = "Y" THEN
            MKDIR DestDir$      'Make the directory.
            PRINT DestDir$; " directory created."
            RESUME NEXT
        ELSE
            PRINT DestDir$; " directory NOT created."
        END IF                  'Return control to caller.
    END IF
    RESUME NEXT                 'Just continue.
END SUB
 
</pre>
</body>

</html>
