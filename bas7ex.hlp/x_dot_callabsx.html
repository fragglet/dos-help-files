<html>

<head>
<title>Absolute Routine Programming Example - bas7ex.hlp</title>
<link rel="stylesheet" href="../style.css">
<link rel="shortcut icon" href="../helpicon.png">
</head>

<body>
<div class="page-heading">
<form action="https://www.google.com/search" method="GET">
<input name="as_q" id="search-box" placeholder="Search">
<input type="hidden" name="as_sitesearch" value="fragglet.github.io/dos-help-files/*">
</form>
bas7ex.hlp (<a href='TOPIC_LIST.html'>Topic list</a>)</div>
<div class="page-title"> Absolute Routine Programming Example</div>
<pre>                       <span class='grhilite'>◄</span>Example<span class='grhilite'>►</span>                 <span class='grhilite'>◄</span><a href='unknown#-9996'>Contents</a><span class='grhilite'>►</span>  <span class='grhilite'>◄</span><a href='unknown#-9997'>Index</a><span class='grhilite'>►</span>  <span class='grhilite'>◄</span><a href='javascript:history.back();'>Back</a><span class='grhilite'>►</span>
──────────────────────────────────────────────────────────────────────────────
'The following example uses the Absolute routine to execute a
'machine-language program stored in an array. The program prints a
'message indicating whether or not a math coprocessor is installed.
 
'Note: To use the Absolute routine, you must load the Quick library
'QBX.QLB using the /L switch when you begin QBX.
 
'AsmBytes is a label; nASMBYTES is a symbolic constant.
CONST nASMBYTES = 14
DEFINT A-Z
DIM AsmProg(1 TO (nASMBYTES / 2))
 
'The machine-language program stored as data to read into the array.
AsmBytes:
DATA &amp;H55              : 'PUSH BP        Save base pointer.
DATA &amp;H8B, &amp;HEC        : 'MOV  BP,SP     Get our own.
DATA &amp;HCD, &amp;H11        : 'INT  11H       Make the ROM-BIOS call.
DATA &amp;H8B, &amp;H5E, &amp;H06  : 'MOV  BX,[BP+6] Get argument address.
DATA &amp;H89, &amp;H07        : 'MOV  [BX],AX   Save list in argument.
DATA &amp;H5D              : 'POP  BP        Restore base pointer.
DATA &amp;HCA, &amp;H02, &amp;H00  : 'RET  2         Pop argument off stack
                         '               and make far return.
'Get the starting offset of the array.
P = VARPTR(AsmProg(1))
'Poke the machine-language program into the array.
DEF SEG = VARSEG(AsmProg(1))   'Change the segment.
FOR I = 0 TO nASMBYTES - 1
   READ J
   POKE (P + I), J
NEXT I
 
'Execute the program. The program expects a single integer argument.
CALL Absolute(X%, VARPTR(AsmProg(1)))
DEF SEG   ' Restore the segment.
'X% now contains bit-encoded equipment list returned by DOS.
'Mask off all but the coprocessor bit (bit 2).
CoProcessor = X% AND &amp;H2
'Print the appropriate message.
IF CoProcessor = 2 THEN
    PRINT "Math coprocessor present."
ELSE
    PRINT "No math coprocessor."
END IF
END
</pre>
</body>

</html>
