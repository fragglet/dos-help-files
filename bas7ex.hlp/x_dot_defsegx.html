<html>

<head>
<title>DEF SEG, PEEK and POKE Programming Example - bas7ex.hlp</title>
<link rel="stylesheet" href="../style.css">
<link rel="shortcut icon" href="../helpicon.png">
</head>

<body>
<div class="page-heading">
<form action="https://www.google.com/search" method="GET">
<input name="as_q" id="search-box" placeholder="Search">
<input type="hidden" name="as_sitesearch" value="fragglet.github.io/dos-help-files/*">
</form>
bas7ex.hlp (<a href='TOPIC_LIST.html'>Topic list</a>)</div>
<div class="notice">
  <img src="../warning.png">
  <div class="heading">
    Important Notice
  </div>
  <div class="text">
    The pages on this site contain documentation for very old MS-DOS software,
    purely for historical purposes.

    If you're looking for up-to-date documentation, particularly for programming,
    you should not rely on the information found here, as it will be woefully
    out of date.
  </div>
</div>
<div class="page-title"> DEF SEG, PEEK and POKE Programming Example</div>
<pre>                       <span class='grhilite'>◄</span>Example<span class='grhilite'>►</span>                 <span class='grhilite'>◄</span><a href='../unknown.html#-9996'>Contents</a><span class='grhilite'>►</span>  <span class='grhilite'>◄</span><a href='../unknown.html#-9997'>Index</a><span class='grhilite'>►</span>  <span class='grhilite'>◄</span><a href='javascript:history.back();'>Back</a><span class='grhilite'>►</span>
──────────────────────────────────────────────────────────────────────────────
'This example uses the DEF SEG statement, the PEEK function, and POKE
'statement to turn the Caps Lock key on and off. The program uses the
'LINE INPUT statement to prompt the user to enter a string, demonstrating
'that the Caps Lock key is on.
 
'Note: This program contains hardware-specific instructions. It works
'correctly on IBM PC, XT, and AT computers.
 
DECLARE SUB CapsOn ()
DECLARE SUB CapsOff ()
DECLARE SUB PrntMsg (R%, C%, M$)
 
CLS
CapsOn
PrntMsg 24, 1, "&lt;Caps Lock On&gt;"
LOCATE 12, 10
LINE INPUT "Enter a string (all characters are caps): ", S$
CapsOff
PrntMsg 24, 1, "              "
PrntMsg 25, 1, "Press any key to continue..."
DO WHILE INKEY$ = "": LOOP
CLS
END
 
SUB CapsOff STATIC
'Turn Caps Lock off.
   DEF SEG = 0
   'Set Caps Lock off (turn off bit 6 of &amp;H0417).
   POKE &amp;H417, PEEK(&amp;H417) AND &amp;HBF
   DEF SEG
END SUB
 
SUB CapsOn STATIC
'Turn Caps Lock on.
   'Set segment to low memory.
   DEF SEG = 0
   'Set Caps Lock on (turn on bit 6 of &amp;H0417).
   POKE &amp;H417, PEEK(&amp;H417) OR &amp;H40
   'Restore segment.
   DEF SEG
END SUB
 
SUB PrntMsg (Row%, Col%, Message$) STATIC
   'Print message at Row%, Col% without changing cursor.
   'Save cursor position.
   CurRow% = CSRLIN: CurCol% = POS(0)
   LOCATE Row%, Col%: PRINT Message$;
   'Restore cursor.
   LOCATE CurRow%, CurCol%
END SUB
</pre>
</body>

</html>
