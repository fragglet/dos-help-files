<html>

<head>
<title>SYD# Function Programming Example - bas7ex.hlp</title>
<link rel="stylesheet" href="../style.css">
<link rel="shortcut icon" href="../helpicon.png">
</head>

<body>
<div class="page-heading">
<form action="https://www.google.com/search" method="GET">
<input name="as_q" id="search-box" placeholder="Search">
<input type="hidden" name="as_sitesearch" value="fragglet.github.io/dos-help-files/*">
</form>
bas7ex.hlp (<a href='TOPIC_LIST.html'>Topic list</a>)</div>
<div class="page-title"> SYD# Function Programming Example</div>
<pre>                       <span class='grhilite'>◄</span>Example<span class='grhilite'>►</span>                 <span class='grhilite'>◄</span><a href='unknown#-9996'>Contents</a><span class='grhilite'>►</span>  <span class='grhilite'>◄</span><a href='unknown#-9997'>Index</a><span class='grhilite'>►</span>  <span class='grhilite'>◄</span><a href='javascript:history.back();'>Back</a><span class='grhilite'>►</span>
──────────────────────────────────────────────────────────────────────────────
'This example uses the SYD# function to return the depreciation of an
'asset for a specific period using sum-of-years' digits method based
'on the asset's initial cost, salvage value, and useful life.
 
'Note: To run this example you must use a Quick library that includes
'the procedures contained in the date/time/format and financial function
'library files.  The following include files must also be present.
 
'$INCLUDE: 'FINANC.BI'
'$INCLUDE: 'FORMAT.BI'
 
CONST YEARMONTHS = 12
CONST DOLLARFORMAT$ = "$#,###,##0.00"
DEFDBL A-Z
DIM Status  AS INTEGER
 
CLS
PRINT "Assume you have a piece of expensive manufacturing equipment."
INPUT "Enter the original cost of the equipment"; Cost
PRINT
PRINT "Salvage value is the value of the asset"
PRINT "at the end of its useful life."
INPUT "What is the salvage value of the equipment"; SalvageVal
PRINT
INPUT "What is the useful life of the equipment in months"; Life
IF Life &lt; YEARMONTHS THEN
    PRINT
    PRINT "If the life is less than a year, it is not an asset."
    INPUT "Reenter the useful life of the equipment in months"; Life
END IF
 
YearsInLife = Life / YEARMONTHS
 
'Round up to a whole year.
IF YearsInLife &lt;&gt; INT(Life / YEARMONTHS) THEN
    YearsInLife = INT(YearsInLife + 1)
END IF
 
PRINT
PRINT "For what year of the asset's life do you want to calculate ";
INPUT "depreciation "; DeprYear
DO WHILE Depr &lt; YearsInLife AND DeprYear &gt; YearsInLife
    PRINT
    PRINT "The year you enter must be at least 1, and not more than";
    PRINT YearsInLife
    INPUT "Reenter the year"; DeprYear
LOOP
PRINT
 
'Calculate and format the results.
PeriodDepr = SYD#(Cost, SalvageVal, YearsInLife, DeprYear, Status)
 
'Examine Status to determine success of failure of SYD#.
IF Status THEN
    'If unsuccessful, announce a problem.
    PRINT "There was an error in calculating depreciation."
ELSE
    'If successful, calculate, format, and display the results.
    TotalDepr = Cost - SalvageVal
    MonthDepr = PeriodDepr / YEARMONTHS
 
    Cost$ = FormatD$(Cost, DOLLARFORMAT$)
    SalvageVal$ = FormatD$(SalvageVal, DOLLARFORMAT$)
    TotalDepr$ = FormatD$(TotalDepr, DOLLARFORMAT$)
    PeriodDepr$ = FormatD$(PeriodDepr, DOLLARFORMAT$)
    MonthDepr$ = FormatD$(MonthDepr, DOLLARFORMAT$)
 
    PRINT "If the original cost of your equipment is "; Cost$; ","
    PRINT "and the salvage value of that equipment is "; SalvageVal$; ","
    PRINT "the total depreciation at the end of its"; Life; "month"
    PRINT "lifetime is "; TotalDepr$; ". ";
    PRINT "The depreciation in year"; DeprYear
    PRINT "is "; PeriodDepr$; " or "; MonthDepr$; " per month."
END IF
</pre>
</body>

</html>
